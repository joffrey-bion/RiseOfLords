apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'launch4j'

group = 'com.jbion.bots'
version = '1.0.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'org.jsoup:jsoup:1.7.3'
    testCompile 'junit:junit:4.+'
}

String mainClassName = "com.jbion.riseoflords.Main"

// to build a fat jar with all dependencies included
jar {
    manifest { attributes "Main-Class": mainClassName }
    from { 
    	configurations.compile.collect { 
    		it.isDirectory() ? it : zipTree(it) 
    	}
    }
}

// wraps the executable jar into an exe file
launch4j {
    mainClassName = "com.jbion.riseoflords.Main"
    headerType = "console"
    jar = 'lib/' + project.jar.archiveName
    icon = '../resources/main/rol-bot.ico'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'edu.sc.seis.gradle:launch4j:1.0.6'
    }
}

task distExe(type: Copy) {
	dependsOn tasks.launch4j
	from(project.buildDir.path + '/' + project.launch4j.outputDir)
	into('dist')
	include(project.launch4j.outfile)
}