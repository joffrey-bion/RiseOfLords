apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'launch4j'
apply plugin: 'application'

group = 'com.jbion.bots'
version = '1.0.2'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    // this flat dir is for the distribution directory
    flatDir {
        name "distRepo"
        dirs "dist"
    }
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'org.jsoup:jsoup:1.7.3'
    testCompile 'junit:junit:4.+'
}

// defines what to distribute (jar and exe)
artifacts {
    archives jar
    archives file(buildDir.name + '/' + launch4j.outputDir + '/' + launch4j.outfile)
}

mainClassName = "com.jbion.riseoflords.Main"

// to build a fat jar with all dependencies included
jar {
    manifest { attributes "Main-Class": mainClassName }
    from { 
    	configurations.compile.collect { 
    		it.isDirectory() ? it : zipTree(it) 
    	}
    }
}

// wraps the executable jar into an exe file
launch4j {
    mainClassName = "com.jbion.riseoflords.Main"
    headerType = "console"
    jar = 'lib/' + project.jar.archiveName
    icon = '../resources/main/rol-bot.ico'
    windowTitle = project.name
}

// specifies were to 'upload' the produced artifacts using our flat dir for distribution
uploadArchives {
    repositories {
        add project.repositories.distRepo
    }
}

// convenient task to do all the work
task dist {
    dependsOn tasks.launch4j, tasks.uploadArchives
}

// loads the launch4j dependency for build time
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'edu.sc.seis.gradle:launch4j:1.0.6'
    }
}